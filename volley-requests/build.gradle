apply plugin: 'com.android.library'
apply plugin: 'maven'

version = "1.0.0"
group = "com.monits"

ext.isReleaseVersion = has("release")

if (isReleaseVersion) {
    ext.nexusRepo = 'http://nexus.monits.com/content/repositories/oss-releases'
} else {
    version += "-SNAPSHOT"
    ext.nexusRepo = 'http://nexus.monits.com/content/repositories/oss-snapshots'
}

ext.nexusUsername = has("nexusUsername") ? nexusUsername : ""
ext.nexusPassword = has("nexusPassword") ? nexusPassword : ""


android {
    compileSdkVersion 19
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    compile files('libs/volley.jar')
    compile 'com.android.support:support-v4:21.0.+'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.google.guava:guava:17.0'
}

configurations {
    archives {
        extendsFrom configurations.default
    }
}

// Generate sources
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from 'src/main/java'
}

artifacts {
    archives sourcesJar
}

// To upload artifact to Nexus
uploadArchives {
    configuration = configurations.archives
    repositories {
        mavenDeployer {
            repository(url: project.ext.nexusRepo) {
                authentication(userName: project.ext.nexusUsername, password: project.ext.nexusPassword)
            }

            pom.project {
                name 'Volley Requests'
                packaging 'aar'
                description 'Extension library for common use cases with Android Volley'
                url 'https://github.com/Monits/volley-requests'

                scm {
                    url 'scm:git@github.com:Monits/volley-requests.git'
                    connection 'scm:git@github.com:Monits/volley-requests.git'
                    developerConnection 'scm:git@github.com:Monits/volley-requests.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'jsotuyod'
                        name 'Juan Martin Sotuyo Dodero'
                        email 'jmsotuyo@monits.com'
                    }
                    developer {
                        id 'MartuPuri'
                        name 'Martin Purita'
                        email 'mpurita@monits.com'
                    }
                    developer {
                        id 'DTecheira'
                        name 'Damián Techeira'
                        email 'dtecheira@monits.com'
                    }
                    developer {
                        id 'GMuniz'
                        name 'Gastón Muñiz'
                    }
                }
            }
        }
    }
}