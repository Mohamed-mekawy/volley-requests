apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

version = "1.1.0-SNAPSHOT"
group = "com.monits"

if (version.endsWith('-SNAPSHOT')) {
    ext.nexusRepo = 'http://nexus.monits.com/content/repositories/oss-snapshots'
} else {
    ext.nexusRepo = 'http://nexus.monits.com/content/repositories/oss-releases'
}

ext.nexusUsername = has("nexusUsername") ? nexusUsername : ""
ext.nexusPassword = has("nexusPassword") ? nexusPassword : ""


android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        versionName project.version
        minSdkVersion 8
        targetSdkVersion 22
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile files('libs/volley.jar')
    compile 'com.android.support:support-v4:22.2.0'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.google.guava:guava:18.0'
    provided 'com.google.code.findbugs:annotations:3.0.0'
    checkstyle 'com.puppycrawl.tools:checkstyle:6.7'
    findbugs 'com.google.code.findbugs:findbugs:3.0.1'
    findbugs configurations.findbugsPlugins.dependencies
    // To keep everything tidy, we set these apart
    findbugsPlugins('com.monits:findbugs-plugin:0.2.0-SNAPSHOT') {
        transitive = false
    }
    findbugsPlugins 'com.mebigfatguy.fb-contrib:fb-contrib:6.2.0'
    // Robolectric
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.hamcrest:hamcrest-integration:1.3'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'org.apache.maven:maven-ant-tasks:2.1.3'	// for unknown reasons, CI server can't find this on it's own..
    testCompile('org.robolectric:robolectric:2.4') {
        exclude module: 'classworlds'
        exclude module: 'commons-logging'
        exclude module: 'httpclient'
        exclude module: 'maven-artifact'
        exclude module: 'maven-artifact-manager'
        exclude module: 'maven-error-diagnostics'
        exclude module: 'maven-model'
        exclude module: 'maven-project'
        exclude module: 'maven-settings'
        exclude module: 'plexus-container-default'
        exclude module: 'plexus-interpolation'
        exclude module: 'plexus-utils'
        exclude module: 'wagon-file'
        exclude module: 'wagon-http-lightweight'
        exclude module: 'wagon-provider-api'
    }
}

configurations {
    archives {
        extendsFrom configurations.default
    }
    provided {
        dependencies.all { dep ->
            configurations.default.exclude group: dep.group, module: dep.name
        }
    }
    compile.extendsFrom provided
}

// linters!
task downloadCheckstyleXml {
    new File("${rootProject.projectDir}/config/checkstyle/").mkdirs()
    ant.get(src: 'http://static.monits.com/checkstyle.xml', dest: "${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
}

checkstyle {
    configFile file("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
}

task checkstyle(type: Checkstyle, dependsOn: downloadCheckstyleXml) {
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = project.configurations.compile
}

task findbugs(type: FindBugs) {
    dependsOn project.tasks.withType(JavaCompile)

    effort = "max"

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml {
            destination "$project.buildDir/reports/findbugs/findbugs.xml"
            xml.withMessages true
        }
    }

    pluginClasspath = project.configurations.findbugsPlugins
    classpath = project.configurations.compile
}

// when using 'android-unit-test' we need to set it like this...
tasks.withType(FindBugs) {
    ignoreFailures = true
    excludeFilter = file("$rootProject.projectDir/config/findbugs/excludeFilter.xml")
    classes = files("$project.buildDir/intermediates/classes/")
}

pmd {
    toolVersion = '5.3.2'
}

task pmd(type: Pmd) {
    ignoreFailures = true

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = true
        html.enabled = false
    }

    ruleSets = ['http://static.monits.com/pmd.xml']
}

// add CPD to check
check << {
    File outDir = new File("$project.buildDir/reports/pmd/")
    outDir.mkdirs()
    ant.taskdef(name: 'cpd', classname: 'net.sourceforge.pmd.cpd.CPDTask',
            classpath: configurations.pmd.asPath)
    ant.cpd(minimumTokenCount: '100', format: 'xml',
            outputFile: new File(outDir , 'cpd.xml')) {
        fileset(dir: "src") {
            include(name: '**/*.java')
            exclude(name: '**/gen/**')
        }
    }
}

check.dependsOn 'checkstyle', 'pmd', 'findbugs'

// Code coverage!
jacoco {
    toolVersion '0.7.2.201409121644'
}

task jacocoTestReport(type: JacocoReport, dependsOn: 'test') {
    group = 'Reporting'
    description = 'Generate Jacoco coverage reports after running tests.'
    reports {
        xml.enabled = true
    }
    classDirectories = fileTree(
        dir: "$project.buildDir/intermediates/classes/debug",
        excludes: ['**/R\$*.class'])
    sourceDirectories = files(['src/main/java'])
    executionData = files("$buildDir/jacoco/testDebug.exec")
}

coveralls {
    jacocoReportPath = "$project.buildDir/reports/jacoco/jacocoTestReport/jacocoTestReport.xml"
}

// Generate sources
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from 'src/main/java'
}

artifacts {
    archives sourcesJar
}

// To upload artifact to Nexus
uploadArchives {
    configuration = configurations.archives
    repositories {
        mavenDeployer {
            repository(url: project.ext.nexusRepo) {
                authentication(userName: project.ext.nexusUsername, password: project.ext.nexusPassword)
            }

            pom.project {
                name 'Volley Requests'
                packaging 'aar'
                description 'Extension library for common use cases with Android Volley'
                url 'https://github.com/Monits/volley-requests'

                scm {
                    url 'scm:git@github.com:Monits/volley-requests.git'
                    connection 'scm:git@github.com:Monits/volley-requests.git'
                    developerConnection 'scm:git@github.com:Monits/volley-requests.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'jsotuyod'
                        name 'Juan Martin Sotuyo Dodero'
                        email 'jmsotuyo@monits.com'
                    }
                    developer {
                        id 'MartuPuri'
                        name 'Martin Purita'
                        email 'mpurita@monits.com'
                    }
                    developer {
                        id 'DTecheira'
                        name 'Damián Techeira'
                        email 'dtecheira@monits.com'
                    }
                    developer {
                        id 'GMuniz'
                        name 'Gastón Muñiz'
                    }
                }
            }
        }
    }
}
